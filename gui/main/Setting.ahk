#Requires AutoHotkey v2.0

global gSettingGui := Gui("-MinimizeBox -MaximizeBox")
global gSettingCtrls := Map()
global gSettingLayout := SettingLayout.Window()

UiApplyWindow(gSettingGui)
gSettingGui.OnEvent("Escape", SettingGuiEscape)
gSettingGui.OnEvent("Close", SettingGuiClose)

gSettingCtrls["Tab"] := gSettingGui.Add("Tab3", UiLayoutRect(gSettingLayout, 0, 0, SettingLayout.TabWidth(), SettingLayout.TabHeight()), [MainText["SettingTabGeneral"], MainText["SettingTabHelp"], MainText["SettingTabAbout"]])
gSettingCtrls["Tab"].UseTab(MainText["SettingTabGeneral"])
gSettingCtrls["SettingAutoStart"] := gSettingGui.Add("CheckBox", "vSettingAutoStart x16 y32 h20", MainText["SettingAutoStart"])
gSettingCtrls["SettingOnSystemStart"] := gSettingGui.Add("CheckBox", "vSettingOnSystemStart x16 y54 h20", MainText["SettingOnSystemStart"])
gSettingCtrls["SettingBlockWin"] := gSettingGui.Add("CheckBox", "vSettingBlockWin x16 y76 h20", MainText["SettingBlockWin"])
gSettingCtrls["SettingSubprocessErrorLog"] := gSettingGui.Add("CheckBox", "vSettingSubprocessErrorLog x16 y98 h20", MainText["SettingSubprocessErrorLog"])
gSettingCtrls["SettingCloseToTray"] := gSettingGui.Add("CheckBox", "vSettingCloseToTray x16 y120 h20", MainText["SettingCloseToTray"])
gSettingGui.Add("Text", "x16 y150 w120 h22 +0x200", MainText["SettingGlobalPauseHotkey"])
gSettingCtrls["SettingGlobalPauseHotkey"] := gSettingGui.Add("Hotkey", "vSettingGlobalPauseHotkey x142 y150 w120 h22")
gSettingGui.Add("Button", "x310 y250 w80 h40", MainText["Save"]).OnEvent("Click", SettingSave)
gSettingCtrls["Tab"].UseTab(MainText["SettingTabHelp"])
gSettingGui.Add("Text", "x16 y32 w368 h268", MainText["SettingHelp"])
gSettingCtrls["Tab"].UseTab(MainText["SettingTabAbout"])
gSettingGui.Add("Text", "x16 y32 w368 h24 +0x200", MainText["CurrentMaintainedPost"])
gSettingGui.Add("Link", "x16 y54 w368 h24", "<a href=`"" MainText["CurrentMaintainedPostUrl"] "`">" MainText["CurrentMaintainedPostUrl"] "</a>")
gSettingGui.Add("Text", "x16 y88 w368 h24 +0x200", MainText["Source"])
gSettingGui.Add("Link", "x16 y110 w368 h24", "<a href=`"" MainText["SourceUrl"] "`">" MainText["SourceUrl"] "</a>")
gSettingGui.Add("Text", "x16 y132 w368 h24 +0x200", MainText["OriginalPost"])
gSettingGui.Add("Link", "x16 y154 w368 h24", "<a href=`"" MainText["OriginalPostUrl"] "`">" MainText["OriginalPostUrl"] "</a>")
gSettingCtrls["Tab"].UseTab()

SettingGetCtrl(name) {
    global gSettingCtrls
    return gSettingCtrls.Has(name) ? gSettingCtrls[name] : ""
}

SettingGuiEscape(*) {
    HideGuiSetting()
}

SettingGuiClose(*) {
    HideGuiSetting()
}

ShowGuiSetting(*) {
    global gMainGui, gSettingGui, gSettingLayout
    DisableGuiMain()
    if IsObject(gMainGui) {
        gSettingGui.Opt("+Owner" gMainGui.Hwnd)
    }
    gSettingGui.Title := MainText["Setting"]
    gSettingGui.Show("w" gSettingLayout.Width() " h" gSettingLayout.Height())
    SettingLoad()
}

HideGuiSetting() {
    gSettingGui.Hide()
    EnableGuiMain()
}

SettingSave(*) {
    global _OnSystemStart, _BlockWin, _CloseToTray, _GlobalPauseHotkey
    settingAutoStart := SettingGetCtrl("SettingAutoStart").Value
    settingOnSystemStart := SettingGetCtrl("SettingOnSystemStart").Value
    settingBlockWin := SettingGetCtrl("SettingBlockWin").Value
    settingSubprocessErrorLog := SettingGetCtrl("SettingSubprocessErrorLog").Value
    settingCloseToTray := SettingGetCtrl("SettingCloseToTray").Value
    settingGlobalPauseHotkey := SettingGetCtrl("SettingGlobalPauseHotkey").Value

    SaveConfig("SettingAutoStart", settingAutoStart)
    SaveConfig("SettingOnSystemStart", settingOnSystemStart)
    SaveConfig("SettingBlockWin", settingBlockWin)
    SaveConfig("SettingSubprocessErrorLog", settingSubprocessErrorLog)
    SaveConfig("SettingCloseToTray", settingCloseToTray)
    SaveConfig("SettingGlobalPauseHotkey", settingGlobalPauseHotkey)

    _OnSystemStart := settingOnSystemStart
    _BlockWin := settingBlockWin
    _CloseToTray := settingCloseToTray
    _GlobalPauseHotkey := settingGlobalPauseHotkey

    SettingNow()
    GlobalPause_RegisterHotkey(_GlobalPauseHotkey)
    HideGuiSetting()
}

SettingLoad() {
    SettingGetCtrl("SettingAutoStart").Value := LoadConfig("SettingAutoStart", false)
    SettingGetCtrl("SettingOnSystemStart").Value := LoadConfig("SettingOnSystemStart", false)
    SettingGetCtrl("SettingBlockWin").Value := LoadConfig("SettingBlockWin", false)
    SettingGetCtrl("SettingSubprocessErrorLog").Value := LoadConfig("SettingSubprocessErrorLog", false)
    SettingGetCtrl("SettingCloseToTray").Value := LoadConfig("SettingCloseToTray", false)
    SettingGetCtrl("SettingGlobalPauseHotkey").Value := LoadConfig("SettingGlobalPauseHotkey", "F11")
}

SettingNow() {
    if (_OnSystemStart) {
        FileCreateShortcut(A_ScriptFullPath, A_Startup "\" MainText["StartupShortcutName"])
    } else {
        try FileDelete(A_Startup "\" MainText["StartupShortcutName"])
    }
    if (_BlockWin) {
        Hotkey("$*LWin", BlockWin, "On")
        Hotkey("$*RWin", BlockWin, "On")
    } else {
        try Hotkey("$*LWin", "Off")
        try Hotkey("$*RWin", "Off")
    }
}

BlockWin(*) {
}

GlobalPause_RegisterHotkey(hotkeyName) {
    global __GlobalPauseHotkeyId
    try {
        HotIfWinActive("ahk_group DNF")
        if (__GlobalPauseHotkeyId != "") {
            try Hotkey(__GlobalPauseHotkeyId, "Off")
        }
        __GlobalPauseHotkeyId := ""
        hotkeyName := Trim(hotkeyName)
        if (hotkeyName != "") {
            __GlobalPauseHotkeyId := "~$" hotkeyName
            Hotkey(__GlobalPauseHotkeyId, GlobalPause_OnHotkey, "On")
        }
        HotIf()
    } catch {
        try HotIf()
    }
}

GlobalPause_OnHotkey(*) {
    paused := GlobalPause_Toggle()
    ShowTip(MainText[paused ? "GlobalPauseOn" : "GlobalPauseOff"])
}

global _AutoStart := LoadConfig("SettingAutoStart", false)
global _OnSystemStart := LoadConfig("SettingOnSystemStart", false)
global _BlockWin := LoadConfig("SettingBlockWin", false)
global _CloseToTray := LoadConfig("SettingCloseToTray", false)
global _GlobalPauseHotkey := LoadConfig("SettingGlobalPauseHotkey", "F11")
global __GlobalPauseHotkeyId := ""

if (_BlockWin) {
    Hotkey("$*LWin", BlockWin, "On")
    Hotkey("$*RWin", BlockWin, "On")
}
GlobalPause_RegisterHotkey(_GlobalPauseHotkey)
