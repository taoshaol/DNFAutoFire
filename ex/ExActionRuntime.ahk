#Requires AutoHotkey v2.0

; EX 输入动作运行时：统一承载战法、旅人、关羽、宠物技能、剑宗、修罗、自动奔跑和一键连招。
; 主键连发与多键并发由 MainAutoFire 独立子进程承载，避免高频主连发被扩展动作影响。

class ExActionRuntime {
    static _ctx := 0

    static Run(presetName := "") {
        ProcessSetPriority("High")
        SetStoreCapsLockMode(false)
        RegisterGameWindowGroup()
        try InstallKeybdHook()
        try UnlockSystemTimeLimit()
        OnExit(ObjBindMethod(ExActionRuntime, "OnExit"))

        presetName := presetName = "" ? ResolvePresetName(LoadLastPreset()) : NormalizePresetName(presetName)
        rules := ExAction_BuildRules(presetName)
        guanYuProfiles := ExAction_BuildGuanYuProfiles(presetName)
        comboProfiles := ExAction_BuildComboProfiles(presetName)
        autoRun := ExAction_BuildAutoRun(presetName)

        if (rules.Length = 0 && guanYuProfiles.Length = 0 && comboProfiles.Length = 0 && !IsObject(autoRun)) {
            return
        }

        this._ctx := {
            rules: rules,
            actionHotkeyIds: [],
            actionHeldScIDs: Map(),
            guanYuProfiles: guanYuProfiles,
            comboProfiles: comboProfiles,
            autoRun: autoRun,
            wasActive: WinActive("ahk_group DNF") != 0
        }

        this._StartRuleTimers()
        this._EnableActionHotkeys()
        this._EnableAutoRunHooks()
        Suspend(false)

        loop {
            this._WatchFocusLoss()
            Sleep(50)
        }
    }

    static _StartRuleTimers() {
        ctx := this._ctx
        for rule in ctx.rules {
            if (rule.policy = "onceOnPressEdge" || rule.policy = "onceOnHoldEdge") {
                continue
            }
            rule.tickFn := ObjBindMethod(ExActionRuntime, "RuleTick", rule)
            SetTimer(rule.tickFn, rule.tickMs)
        }
    }

    static _StopRuleTimers() {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        for rule in ctx.rules {
            try SetTimer(rule.tickFn, 0)
        }
    }

    static _EnableActionHotkeys() {
        ctx := this._ctx
        hotkeys := Map()
        for rule in ctx.rules {
            if IsObject(rule.scIDs) {
                for scID in rule.scIDs {
                    ExAction_MarkHotkey(hotkeys, scID, HasProp(rule, "blockOriginal") && rule.blockOriginal)
                }
            }
        }
        for profile in ctx.guanYuProfiles {
            ExAction_MarkHotkey(hotkeys, profile.scID, false)
        }
        for profile in ctx.comboProfiles {
            ExAction_MarkHotkey(hotkeys, profile.scID, profile.blockOriginal)
        }
        for scID, blockOriginal in hotkeys {
            ctx.actionHotkeyIds.Push(scID)
            if blockOriginal {
                HotIf(ExAction_BlockHotkeyActive)
                Hotkey("$" scID, ObjBindMethod(ExActionRuntime, "ActionDownByScID", scID), "On")
                HotIf()
                Hotkey("~$" scID " up", ObjBindMethod(ExActionRuntime, "ActionUpByScID", scID), "On")
            } else {
                HotIfWinActive("ahk_group DNF")
                Hotkey("~$" scID, ObjBindMethod(ExActionRuntime, "ActionDownByScID", scID), "On")
                Hotkey("~$" scID " up", ObjBindMethod(ExActionRuntime, "ActionUpByScID", scID), "On")
                HotIf()
            }
        }
    }

    static _DisableActionHotkeys() {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        for rule in ctx.rules {
            if IsObject(rule.heldScIDs) {
                rule.heldScIDs := Map()
            }
        }
        for profile in ctx.guanYuProfiles {
            try SetTimer(profile.pendingFn, 0)
            profile.pending := false
            profile.isHeld := false
        }
        this.ComboStopAll()
        for scID in ctx.actionHotkeyIds {
            try {
                HotIf(ExAction_BlockHotkeyActive)
                try Hotkey("$" scID, "Off")
                HotIf()
                try Hotkey("~$" scID " up", "Off")
                HotIfWinActive("ahk_group DNF")
                try Hotkey("~$" scID, "Off")
                try Hotkey("~$" scID " up", "Off")
                HotIf()
            } catch {
                try HotIf()
            }
        }
        ctx.actionHeldScIDs := Map()
        ctx.actionHotkeyIds := []
    }

    static ActionDownByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) || !WinActive("ahk_group DNF") {
            return
        }
        if (ctx.actionHeldScIDs.Has(scID) && ctx.actionHeldScIDs[scID]) {
            return
        }
        ctx.actionHeldScIDs[scID] := true
        this.RuleDownByScID(scID)
        this.GuanYuDownByScID(scID)
        this.ComboDownByScID(scID)
    }

    static ActionUpByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        if (!ExAction_IsLockScID(scID) && GetKeyState(scID, "P")) {
            return
        }
        if IsObject(ctx.actionHeldScIDs) {
            ctx.actionHeldScIDs[scID] := false
        }
        this.RuleUpByScID(scID)
        this.GuanYuUpByScID(scID)
        this.ComboUpByScID(scID)
    }

    static RuleDownByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) || !WinActive("ahk_group DNF") {
            return
        }
        for rule in ctx.rules {
            if !ExAction_RuleHasScID(rule, scID) {
                continue
            }
            if (rule.heldScIDs.Has(scID) && rule.heldScIDs[scID]) {
                continue
            }
            rule.heldScIDs[scID] := true
            if (rule.policy = "onceOnPressEdge" || rule.policy = "onceOnHoldEdge") {
                SendIP(rule.sendToken)
            }
        }
    }

    static RuleUpByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        for rule in ctx.rules {
            if IsObject(rule.heldScIDs) && rule.heldScIDs.Has(scID) {
                rule.heldScIDs[scID] := false
            }
        }
    }

    static GuanYuDownByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        loop ctx.guanYuProfiles.Length {
            if ctx.guanYuProfiles.Has(A_Index) && ctx.guanYuProfiles[A_Index].scID = scID {
                this.GuanYuDown(A_Index)
            }
        }
    }

    static GuanYuUpByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        loop ctx.guanYuProfiles.Length {
            if ctx.guanYuProfiles.Has(A_Index) && ctx.guanYuProfiles[A_Index].scID = scID {
                this.GuanYuUp(A_Index)
            }
        }
    }

    static GuanYuDown(profileIdx, *) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.guanYuProfiles.Length || !ctx.guanYuProfiles.Has(profileIdx) {
            return
        }
        profile := ctx.guanYuProfiles[profileIdx]
        if profile.isHeld {
            return
        }
        profile.isHeld := true
        profile.pending := true
        SetTimer(profile.pendingFn, 0)
        SetTimer(profile.pendingFn, -profile.leadDelayMs)
    }

    static GuanYuUp(profileIdx, *) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.guanYuProfiles.Length || !ctx.guanYuProfiles.Has(profileIdx) {
            return
        }
        profile := ctx.guanYuProfiles[profileIdx]
        profile.isHeld := false
        if profile.pending {
            try SetTimer(profile.pendingFn, 0)
            profile.pending := false
        }
    }

    static GuanYuSend(profileIdx, *) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.guanYuProfiles.Length || !ctx.guanYuProfiles.Has(profileIdx) {
            return
        }
        profile := ctx.guanYuProfiles[profileIdx]
        profile.pending := false
        if !WinActive("ahk_group DNF") {
            return
        }
        ExAction_RunSequence(profile)
    }

    static RuleTick(rule, *) {
        if (rule.busy) {
            return
        }
        rule.busy := true
        try {
            this._RuleTickCore(rule)
        } finally {
            rule.busy := false
        }
    }

    static _RuleTickCore(rule) {
        if !WinActive("ahk_group DNF") {
            ExAction_ResetRuleForInactive(rule)
            return
        }

        anyHeld := ExAction_RuleAnyHeld(rule)
        switch rule.policy {
        case "repeatWhileAnyHeld":
            if anyHeld {
                SendIP(rule.sendToken)
            }
        case "repeatAfterHoldDelay":
            if !anyHeld {
                ExAction_ResetHoldRule(rule)
                rule.heldLast := false
            } else {
                if !rule.heldLast {
                    rule.pendingStartTick := A_TickCount
                    rule.heldLast := true
                    return
                }
                if (A_TickCount - rule.pendingStartTick >= rule.delayMs) {
                    SendIP(rule.sendToken)
                }
            }
        case "xiuLuoBurstRepeat":
            if !anyHeld {
                rule.heldLast := false
                rule.lastXTick := 0
                rule.lastWaveTick := 0
                return
            }
            nowTick := A_TickCount
            if (!rule.heldLast || rule.lastXTick = 0 || nowTick - rule.lastXTick >= rule.fastMs) {
                SendIP(rule.xSendToken)
                rule.lastXTick := nowTick
            }
            if (!rule.heldLast || rule.lastWaveTick = 0 || nowTick - rule.lastWaveTick >= rule.slowMs) {
                for sendToken in rule.waveSendTokens {
                    SendIP(sendToken)
                }
                rule.lastWaveTick := nowTick
            }
            rule.heldLast := true
        }
    }

    static ComboDownByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        loop ctx.comboProfiles.Length {
            if ctx.comboProfiles.Has(A_Index) && ctx.comboProfiles[A_Index].scID = scID {
                profile := ctx.comboProfiles[A_Index]
                if profile.isHeld {
                    continue
                }
                profile.isHeld := true
                this.ComboStart(A_Index)
            }
        }
    }

    static ComboUpByScID(scID, *) {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        loop ctx.comboProfiles.Length {
            if ctx.comboProfiles.Has(A_Index) && ctx.comboProfiles[A_Index].scID = scID {
                this.ComboUp(A_Index)
            }
        }
    }

    static ComboUp(profileIdx, *) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.comboProfiles.Length || !ctx.comboProfiles.Has(profileIdx) {
            return
        }
        profile := ctx.comboProfiles[profileIdx]
        profile.isHeld := false
        if profile.loop {
            this.ComboStop(profileIdx)
        }
    }

    static ComboStart(profileIdx) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.comboProfiles.Length || !ctx.comboProfiles.Has(profileIdx) {
            return
        }
        if !WinActive("ahk_group DNF") {
            return
        }
        this.ComboStop(profileIdx)
        profile := ctx.comboProfiles[profileIdx]
        profile.running := true
        this.ComboSchedule(profileIdx, 1, 1, profile.runId)
    }

    static ComboClearPendingTimer(profileIdx) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.comboProfiles.Length || !ctx.comboProfiles.Has(profileIdx) {
            return
        }
        profile := ctx.comboProfiles[profileIdx]
        if (profile.pendingTimer != "") {
            try SetTimer(profile.pendingTimer, 0)
            profile.pendingTimer := ""
        }
    }

    static ComboSchedule(profileIdx, skillIdx, delayMs, runId) {
        ctx := this._ctx
        if !this.ComboIsRunning(profileIdx, runId) {
            return
        }
        this.ComboClearPendingTimer(profileIdx)
        profile := ctx.comboProfiles[profileIdx]
        fn := ObjBindMethod(ExActionRuntime, "ComboSendSkillAt", profileIdx, skillIdx, runId)
        profile.pendingTimer := fn
        SetTimer(fn, -delayMs)
    }

    static ComboIsRunning(profileIdx, runId) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.comboProfiles.Length || !ctx.comboProfiles.Has(profileIdx) {
            return false
        }
        profile := ctx.comboProfiles[profileIdx]
        if !profile.running || profile.runId != runId || !WinActive("ahk_group DNF") {
            return false
        }
        return !profile.loop || profile.isHeld
    }

    static ComboSendSkillAt(profileIdx, idx, runId, *) {
        ctx := this._ctx
        if !this.ComboIsRunning(profileIdx, runId) {
            this.ComboStopRun(profileIdx, runId)
            return
        }
        profile := ctx.comboProfiles[profileIdx]
        if (idx > profile.skills.Length || !profile.skills.Has(idx)) {
            this.ComboChainComplete(profileIdx, runId)
            return
        }
        item := profile.skills[idx]
        if !IsObject(item) {
            this.ComboSendSkillAt(profileIdx, idx + 1, runId)
            return
        }
        if (item.sendToken != "") {
            try SendIP(item.sendToken, item.hold)
        }
        delay := item.delay + 0
        if (delay <= 0) {
            this.ComboSendSkillAt(profileIdx, idx + 1, runId)
            return
        }
        this.ComboSchedule(profileIdx, idx + 1, delay, runId)
    }

    static ComboChainComplete(profileIdx, runId, *) {
        ctx := this._ctx
        if !this.ComboIsRunning(profileIdx, runId) {
            this.ComboStopRun(profileIdx, runId)
            return
        }
        profile := ctx.comboProfiles[profileIdx]
        if (profile.loop && profile.isHeld) {
            if (profile.mainIntervalMs > 0) {
                this.ComboSchedule(profileIdx, 1, profile.mainIntervalMs, runId)
            } else {
                this.ComboSendSkillAt(profileIdx, 1, runId)
            }
            return
        }
        this.ComboStopRun(profileIdx, runId)
    }

    static ComboStopRun(profileIdx, runId) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.comboProfiles.Length || !ctx.comboProfiles.Has(profileIdx) {
            return
        }
        if (ctx.comboProfiles[profileIdx].runId = runId) {
            this.ComboStop(profileIdx)
        }
    }

    static ComboStop(profileIdx) {
        ctx := this._ctx
        if !IsObject(ctx) || profileIdx < 1 || profileIdx > ctx.comboProfiles.Length || !ctx.comboProfiles.Has(profileIdx) {
            return
        }
        profile := ctx.comboProfiles[profileIdx]
        this.ComboClearPendingTimer(profileIdx)
        profile.running := false
        profile.runId += 1
    }

    static ComboStopAll() {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        loop ctx.comboProfiles.Length {
            if ctx.comboProfiles.Has(A_Index) {
                ctx.comboProfiles[A_Index].isHeld := false
                this.ComboStop(A_Index)
            }
        }
    }

    static _EnableAutoRunHooks() {
        ctx := this._ctx
        if !IsObject(ctx.autoRun) {
            return
        }
        ar := ctx.autoRun
        HotIfWinActive("ahk_group DNF")
        Hotkey("~$" ar.rightKey, ObjBindMethod(ExActionRuntime, "AutoRunRightDown"), "On")
        Hotkey("~$" ar.rightKey " Up", ObjBindMethod(ExActionRuntime, "AutoRunRightUp"), "On")
        Hotkey("~$" ar.leftKey, ObjBindMethod(ExActionRuntime, "AutoRunLeftDown"), "On")
        Hotkey("~$" ar.leftKey " Up", ObjBindMethod(ExActionRuntime, "AutoRunLeftUp"), "On")
        if (ar.pauseHotkey != "") {
            Hotkey("~$" ar.pauseHotkey, ObjBindMethod(ExActionRuntime, "AutoRunTogglePause"), "On")
        }
        HotIf()
    }

    static _DisableAutoRunHooks() {
        ctx := this._ctx
        if !IsObject(ctx) || !IsObject(ctx.autoRun) {
            return
        }
        ar := ctx.autoRun
        try SetTimer(ar.rightTickFn, 0)
        try SetTimer(ar.leftTickFn, 0)
        try {
            HotIfWinActive("ahk_group DNF")
            try Hotkey("~$" ar.rightKey, "Off")
            try Hotkey("~$" ar.rightKey " Up", "Off")
            try Hotkey("~$" ar.leftKey, "Off")
            try Hotkey("~$" ar.leftKey " Up", "Off")
            if (ar.pauseHotkey != "") {
                try Hotkey("~$" ar.pauseHotkey, "Off")
            }
            HotIf()
        } catch {
            try HotIf()
        }
    }

    static _AutoRunStopActive(ar) {
        ar.pressingRight := false
        ar.pressingLeft := false
        ar.doubleRight := false
        ar.doubleLeft := false
        ar.rightCounter := 0
        ar.leftCounter := 0
        try SetTimer(ar.rightTickFn, 0)
        try SetTimer(ar.leftTickFn, 0)
    }

    static _AutoRunPaused() {
        ar := this._ctx.autoRun
        return ar.paused || GlobalPause_IsPaused()
    }

    static AutoRunTogglePause(*) {
        ar := this._ctx.autoRun
        ar.paused := !ar.paused
    }

    static AutoRunRightDown(*) {
        ar := this._ctx.autoRun
        if this._AutoRunPaused() {
            return
        }
        if !ar.pressingRight {
            ar.pressingRight := true
            ar.doubleRight := false
            ar.rightCounter := 0
            SetTimer(ar.rightTickFn, ar.tickMs)
        }
    }

    static AutoRunRightUp(*) {
        ar := this._ctx.autoRun
        ar.pressingRight := false
        SetTimer(ar.rightTickFn, 0)
        SendEvent(ar.rightUpSend)
    }

    static AutoRunRightTick(*) {
        ar := this._ctx.autoRun
        if this._AutoRunPaused() {
            return
        }
        ar.rightCounter++
        if (ar.pressingRight && !ar.doubleRight) {
            SendEvent(ar.rightPulseSend)
            ar.doubleRight := true
        }
        if (ar.rightCounter >= 3) {
            SetTimer(ar.rightTickFn, 0)
        }
    }

    static AutoRunLeftDown(*) {
        ar := this._ctx.autoRun
        if this._AutoRunPaused() {
            return
        }
        if !ar.pressingLeft {
            ar.pressingLeft := true
            ar.doubleLeft := false
            ar.leftCounter := 0
            SetTimer(ar.leftTickFn, ar.tickMs)
        }
    }

    static AutoRunLeftUp(*) {
        ar := this._ctx.autoRun
        ar.pressingLeft := false
        SetTimer(ar.leftTickFn, 0)
        SendEvent(ar.leftUpSend)
    }

    static AutoRunLeftTick(*) {
        ar := this._ctx.autoRun
        if this._AutoRunPaused() {
            return
        }
        ar.leftCounter++
        if (ar.pressingLeft && !ar.doubleLeft) {
            SendEvent(ar.leftPulseSend)
            ar.doubleLeft := true
        }
        if (ar.leftCounter >= 3) {
            SetTimer(ar.leftTickFn, 0)
        }
    }

    static _WatchFocusLoss() {
        ctx := this._ctx
        if !IsObject(ctx) {
            return
        }
        isActive := WinActive("ahk_group DNF") != 0
        if (ctx.wasActive && !isActive) {
            for rule in ctx.rules {
                ExAction_ResetRuleForInactive(rule)
            }
            ctx.actionHeldScIDs := Map()
            this.ComboStopAll()
            for profile in ctx.guanYuProfiles {
                try SetTimer(profile.pendingFn, 0)
                profile.pending := false
                profile.isHeld := false
            }
            if IsObject(ctx.autoRun) {
                this._AutoRunStopActive(ctx.autoRun)
            }
        }
        ctx.wasActive := isActive
    }

    static OnExit(exitReason, exitCode) {
        this._StopRuleTimers()
        this._DisableActionHotkeys()
        this._DisableAutoRunHooks()
        try RestoreSystemTimeLimit()
    }
}

ExActionRuntime_Run(presetName := "") {
    ExActionRuntime.Run(presetName)
}

ExAction_HasRunnable(presetName) {
    presetName := presetName = "" ? ResolvePresetName(LoadLastPreset()) : NormalizePresetName(presetName)
    return ExAction_BuildRules(presetName).Length > 0
        || ExAction_BuildGuanYuProfiles(presetName).Length > 0
        || ExAction_BuildComboProfiles(presetName).Length > 0
        || IsObject(ExAction_BuildAutoRun(presetName))
}

ExAction_MarkHotkey(hotkeys, scID, blockOriginal) {
    if (scID = "") {
        return
    }
    hotkeys[scID] := (hotkeys.Has(scID) && hotkeys[scID]) || blockOriginal
}

ExAction_BlockHotkeyActive(*) {
    return WinActive("ahk_group DNF") && !GlobalPause_IsPaused() && !ChatOpen_IsOpen()
}

ExAction_BuildRules(presetName) {
    rules := []
    intervalMs := LoadAutoFireGlobalIntervalMs()
    if LoadPreset(presetName, "LvRenState", false) {
        ExAction_AddRepeatRule(rules, "LvRen", LvRenLoadKeys(presetName), LoadPreset(presetName, "LvRenShotKey", "Z"), intervalMs)
    }
    if LoadPreset(presetName, "ZhanFaState", false) {
        ExAction_AddRepeatRule(rules, "ZhanFa", ZhanFaLoadKeys(presetName), LoadPreset(presetName, "ZhanFaShotKey", "Space"), intervalMs)
        ExAction_AddHoldEdgeRule(rules, "ZhanFaBig", ZhanFaLoadKeys(presetName), LoadPreset(presetName, "ZhanFaBigShotKey", ""))
    }
    if LoadPreset(presetName, "PetSkillState", false) {
        ExAction_AddEdgeRule(rules, "PetSkill", PetSkillLoadKeys(presetName), LoadPreset(presetName, "PetSkillShotKey", "Z"))
    }
    if LoadPreset(presetName, "JianZongState", false) {
        skillKey := LoadPreset(presetName, "JianZongSkillKey", "A")
        delayMs := ExAction_Clamp(LoadPreset(presetName, "JianZongDelay", 200), 0, 3000)
        ExAction_AddDelayRepeatRule(rules, "JianZong", [skillKey], skillKey, delayMs, intervalMs)
    }
    if LoadPreset(presetName, "XiuLuoState", false) {
        ExAction_AddXiuLuoRule(
            rules,
            "XiuLuo",
            LoadPreset(presetName, "XiuLuoTriggerKey", ""),
            LoadPreset(presetName, "XiuLuoXKey", "X"),
            [
                LoadPreset(presetName, "XiuLuoWaveKey1", "1"),
                LoadPreset(presetName, "XiuLuoWaveKey2", "2"),
                LoadPreset(presetName, "XiuLuoWaveKey3", "3")
            ],
            intervalMs
        )
    }
    return rules
}

ExAction_BuildGuanYuProfiles(presetName) {
    if !LoadPreset(presetName, "GuanYuState", false) {
        return []
    }
    shotKey := LoadPreset(presetName, "GuanYuShotKey", "Space")
    sendToken := ExAction_SendToken(shotKey)
    if (sendToken = "") {
        return []
    }
    delayMs := ExAction_Clamp(LoadPreset(presetName, "GuanYuDelay", 300), 20, 500)
    skillKeys := GuanYuLoadKeys(presetName)
    skillKeys := ExAction_UniqueKeysByPressKey(skillKeys)
    profiles := []
    for skillKey in skillKeys {
        originKey := GetOriginKeyName(skillKey)
        scID := Key2SC(originKey)
        if (scID = "") {
            continue
        }
        profiles.Push({
            scID: scID,
            leadDelayMs: delayMs,
            isHeld: false,
            pending: false,
            skills: [{ sendToken: sendToken, delay: 0, hold: ComboSkillHoldDefault() }]
        })
        profiles[profiles.Length].pendingFn := ObjBindMethod(ExActionRuntime, "GuanYuSend", profiles.Length)
    }
    return profiles
}

ExAction_AddRepeatRule(rules, name, triggerKeys, shotKey, intervalMs) {
    scIDs := ExAction_BuildScIDs(triggerKeys)
    sendToken := ExAction_SendToken(shotKey)
    if (scIDs.Length = 0 || sendToken = "") {
        return
    }
    rules.Push({
        name: name,
        policy: "repeatWhileAnyHeld",
        scIDs: scIDs,
        heldScIDs: Map(),
        sendToken: sendToken,
        tickMs: ClampMsMin1(intervalMs),
        busy: false,
        pendingStartTick: 0,
        sentForHold: false,
        heldLast: false
    })
}

ExAction_AddEdgeRule(rules, name, triggerKeys, shotKey) {
    scIDs := ExAction_BuildScIDs(triggerKeys)
    sendToken := ExAction_SendToken(shotKey)
    if (scIDs.Length = 0 || sendToken = "") {
        return
    }
    rules.Push({
        name: name,
        policy: "onceOnPressEdge",
        scIDs: scIDs,
        sendToken: sendToken,
        busy: false,
        pendingStartTick: 0,
        sentForHold: false,
        heldLast: false,
        heldScIDs: Map()
    })
}

ExAction_AddHoldEdgeRule(rules, name, triggerKeys, shotKey) {
    scIDs := ExAction_BuildScIDs(triggerKeys)
    sendToken := ExAction_SendToken(shotKey)
    if (scIDs.Length = 0 || sendToken = "") {
        return
    }
    rules.Push({
        name: name,
        policy: "onceOnHoldEdge",
        scIDs: scIDs,
        sendToken: sendToken,
        busy: false,
        pendingStartTick: 0,
        sentForHold: false,
        heldLast: false,
        heldScIDs: Map()
    })
}

ExAction_AddDelayRepeatRule(rules, name, triggerKeys, shotKey, delayMs, intervalMs) {
    scIDs := ExAction_BuildScIDs(triggerKeys)
    sendToken := ExAction_SendToken(shotKey)
    if (scIDs.Length = 0 || sendToken = "") {
        return
    }
    rules.Push({
        name: name,
        policy: "repeatAfterHoldDelay",
        scIDs: scIDs,
        heldScIDs: Map(),
        sendToken: sendToken,
        tickMs: ClampMsMin1(intervalMs),
        busy: false,
        delayMs: delayMs,
        pendingStartTick: 0,
        sentForHold: false,
        heldLast: false
    })
}

ExAction_AddXiuLuoRule(rules, name, triggerKey, xKey, waveKeys, intervalMs) {
    scIDs := ExAction_BuildScIDs([triggerKey])
    xSendToken := ExAction_SendToken(xKey)
    waveSendTokens := []
    for key in waveKeys {
        sendToken := ExAction_SendToken(key)
        if (sendToken != "") {
            waveSendTokens.Push(sendToken)
        }
    }
    if (scIDs.Length = 0 || xSendToken = "" || waveSendTokens.Length = 0) {
        return
    }
    fastMs := ClampMsMin1(intervalMs)
    slowMs := ClampMsMin1(intervalMs * 3)
    rules.Push({
        name: name,
        policy: "xiuLuoBurstRepeat",
        scIDs: scIDs,
        heldScIDs: Map(),
        xSendToken: xSendToken,
        waveSendTokens: waveSendTokens,
        tickMs: fastMs,
        fastMs: fastMs,
        slowMs: slowMs,
        lastXTick: 0,
        lastWaveTick: 0,
        busy: false,
        pendingStartTick: 0,
        sentForHold: false,
        heldLast: false
    })
}

ExAction_BuildComboProfiles(presetName) {
    mainIntervalMs := ExAction_Clamp(LoadPreset(presetName, "MainAutoFireInterval", 20), 1, 200)
    runtimeProfiles := []
    if LoadPreset(presetName, "ComboState", false) {
        profiles := ComboLoadProfilesFromPreset(presetName)
        loop profiles.Length {
            if !profiles.Has(A_Index) {
                continue
            }
            runtime := ExAction_BuildComboProfile(profiles[A_Index], mainIntervalMs)
            if IsObject(runtime) {
                runtimeProfiles.Push(runtime)
            }
        }
    }
    return runtimeProfiles
}

ExAction_BuildComboProfile(profile, mainIntervalMs) {
    if (!IsObject(profile) || !IsObject(profile.skills) || profile.skills.Length == 0) {
        return 0
    }
    triggerKey := ComboCanonMainKey(Trim(String(profile.trigger)))
    if (triggerKey = "") {
        return 0
    }
    scID := Key2SC(GetOriginKeyName(triggerKey))
    if (scID = "") {
        return 0
    }
    skills := []
    for item in profile.skills {
        if !IsObject(item) {
            continue
        }
        skillKey := ComboCanonMainKey(item.key)
        if (skillKey = "") {
            continue
        }
        ; 空技能占位符：保留延迟占位，不发送按键
        if (ComboIsEmptySkillKey(skillKey)) {
            skills.Push({ sendToken: "", delay: ComboNormalizeDelay(item.delay), hold: 0 })
            continue
        }
        sendToken := ExAction_SendToken(skillKey)
        if (sendToken = "") {
            continue
        }
        skills.Push({ sendToken: sendToken, delay: ComboNormalizeDelay(item.delay), hold: ComboNormalizeHold(item.hold) })
    }
    if (skills.Length = 0) {
        return 0
    }
    return {
        scID: scID,
        loop: profile.loop ? true : false,
        blockOriginal: (HasProp(profile, "blockOriginal") && profile.blockOriginal) ? true : false,
        mainIntervalMs: mainIntervalMs,
        isHeld: false,
        running: false,
        runId: 0,
        pendingTimer: "",
        skills: skills
    }
}

ExAction_BuildAutoRun(presetName) {
    if !LoadPreset(presetName, "AutoRunState", false) {
        return 0
    }
    leftKey := LoadPreset(presetName, "AutoRunLeftKey", "Left")
    rightKey := LoadPreset(presetName, "AutoRunRightKey", "Right")
    if (leftKey = "") {
        leftKey := "Left"
    }
    if (rightKey = "") {
        rightKey := "Right"
    }
    tickMs := ExAction_Clamp(LoadPreset(presetName, "AutoRunDelay", 30), 1, 400)
    pauseHotkeyName := Trim(LoadPreset(presetName, "AutoRunPauseHotkey", ""))
    pauseHotkey := pauseHotkeyName = "" ? "" : Key2PressKey(GetOriginKeyName(pauseHotkeyName))
    leftSendKey := ExAction_AutoRunSendKey(leftKey)
    rightSendKey := ExAction_AutoRunSendKey(rightKey)
    ar := {
        leftKey: leftKey,
        rightKey: rightKey,
        pauseHotkey: pauseHotkey,
        paused: false,
        tickMs: tickMs,
        rightPulseSend: "{" rightSendKey " Down}{" rightSendKey " Up}{" rightSendKey " Down}",
        rightUpSend: "{" rightSendKey " Up}",
        leftPulseSend: "{" leftSendKey " Down}{" leftSendKey " Up}{" leftSendKey " Down}",
        leftUpSend: "{" leftSendKey " Up}",
        pressingRight: false,
        doubleRight: false,
        rightCounter: 0,
        pressingLeft: false,
        doubleLeft: false,
        leftCounter: 0
    }
    ar.rightTickFn := ObjBindMethod(ExActionRuntime, "AutoRunRightTick")
    ar.leftTickFn := ObjBindMethod(ExActionRuntime, "AutoRunLeftTick")
    return ar
}

ExAction_AutoRunSendKey(key) {
    key := GetOriginKeyName(key)
    if (key = "Left" || key = "Right" || key = "Up" || key = "Down") {
        return key
    }
    return Key2NoVkSC(key)
}

ExAction_IsLockScID(scID) {
    pk := Format("{:L}", Trim(String(scID)))
    return pk = "sc3a" || pk = "sc45" || pk = "sc46"
}

ExAction_BuildScIDs(keys) {
    scIDs := []
    seen := Map()
    if !IsObject(keys) {
        return scIDs
    }
    for key in keys {
        key := Trim(String(key))
        if (key = "") {
            continue
        }
        scID := Key2SC(GetOriginKeyName(key))
        if (scID = "" || seen.Has(scID)) {
            continue
        }
        seen[scID] := true
        scIDs.Push(scID)
    }
    return scIDs
}

ExAction_RuleHasScID(rule, scID) {
    if !IsObject(rule) || !IsObject(rule.scIDs) {
        return false
    }
    for item in rule.scIDs {
        if (item = scID) {
            return true
        }
    }
    return false
}

ExAction_SendToken(key) {
    key := Trim(String(key))
    if (key = "") {
        return ""
    }
    return Key2NoVkSC(GetOriginKeyName(key))
}

ExAction_RunSequence(profile) {
    if !IsObject(profile) || !IsObject(profile.skills) {
        return
    }
    for item in profile.skills {
        if !IsObject(item) {
            continue
        }
        try SendIP(item.sendToken, item.hold)
        delay := item.delay + 0
        if (delay <= 0) {
            continue
        }
        beginTick := A_TickCount
        while (A_TickCount - beginTick < delay) {
            if !WinActive("ahk_group DNF") {
                return
            }
            Sleep(1)
        }
    }
}

ExAction_UniqueKeysByPressKey(keys) {
    seen := Map()
    out := []
    if !IsObject(keys) {
        return out
    }
    for key in keys {
        key := Trim(String(key))
        if (key = "") {
            continue
        }
        pressKey := Key2PressKey(GetOriginKeyName(key))
        if (pressKey = "" || seen.Has(pressKey)) {
            continue
        }
        seen[pressKey] := true
        out.Push(key)
    }
    return out
}

ExAction_RuleAnyHeld(rule) {
    if !IsObject(rule) || !IsObject(rule.heldScIDs) {
        return false
    }
    for scID, isHeld in rule.heldScIDs {
        if isHeld {
            return true
        }
    }
    return false
}

ExAction_ResetHoldRule(rule) {
    rule.pendingStartTick := 0
    rule.sentForHold := false
}

ExAction_ResetRuleForInactive(rule) {
    ExAction_ResetHoldRule(rule)
    rule.heldLast := false
    if (rule.policy = "xiuLuoBurstRepeat") {
        rule.lastXTick := 0
        rule.lastWaveTick := 0
    }
    if IsObject(rule.heldScIDs) {
        rule.heldScIDs := Map()
    }
}

ExAction_Clamp(value, minValue, maxValue) {
    value := Round(value + 0)
    if (value < minValue) {
        return minValue
    }
    if (value > maxValue) {
        return maxValue
    }
    return value
}

ExAction_LoadKeyList(presetName, configKey) {
    keys := []
    for item in StrSplit(LoadPreset(presetName, configKey), "|") {
        item := Trim(item)
        if (item != "") {
            keys.Push(item)
        }
    }
    return keys
}

LvRenLoadKeys(presetName) {
    return ExAction_LoadKeyList(presetName, "LvRenSkillKeys")
}

ZhanFaLoadKeys(presetName) {
    return ExAction_LoadKeyList(presetName, "ZhanFaSkillKeys")
}

PetSkillLoadKeys(presetName) {
    return ExAction_LoadKeyList(presetName, "PetSkillSkillKeys")
}

GuanYuLoadKeys(presetName) {
    return ExAction_LoadKeyList(presetName, "GuanYuSkillKeys")
}
